marklar sqrt(marklar n) {
	marklar _COMMENT_implementation_used_from___stackoverflow_com_a_1101217;

	marklar op = n;
	marklar res = 0;
	marklar one = 1 << 62;

	while (one > op) {
		one = one >> 2;
	}

	while (one != 0) {
		if (op >= (res + one)) {
			op = op - (res + one);
			res = res + (2 * one);
		}

		res = res >> 1;
		one = one >> 2;
	}

	return res;
}

marklar solveC(marklar a, marklar b) {
	return sqrt((a * a) + (b * b));
}

marklar main() {
	marklar a = 1;
	marklar b = 1;

	while (a < 1000) {
		b = a;
		while (b < 1000) {
			marklar c = solveC(a, b);

			if (c > b) {
				marklar _COMMENT_Verify_that_C_is_a_natural_number;
				if ((c*c) == ((a*a)+(b*b))) {
					marklar sum = a + b + c;

					if (sum == 1000) {
						return ((a*b*c) >> 0);
					}
				}
			}

			b = b + 1;
		}
	
		a = a + 1;
	}

	return 0;
}

